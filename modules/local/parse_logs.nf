// Import generic module functions
process PARSE_LOGS {
    tag "logs"
    label 'process_low'

    conda "bioconda::pandas=1.1.5"
    container "${ workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container ?
        'https://depot.galaxyproject.org/singularity/pandas:1.1.5' :
        'biocontainers/pandas:1.1.5' }"

    input:
    path('filter_by_sequence_quality/*') //PRESTO_FILTERSEQ logs
    path('mask_primers/*') //PRESTO_MASKPRIMERS logs
    path('pair_sequences/*') //PRESTO_PAIRSEQ logs
    path('cluster_sets/*') //PRESTO_CLUTSERSETS logs
    path('build_consensus/*') //PRESTO_BUILDCONSENSUS logs
    path('repair_mates/*') //PRESTO_POSTCONSESUS_PAIRSEQ logs
    path('assemble_pairs/*') //PRESTO_ASSEMBLEPAIRS logs
    path('deduplicates/*') //PRESTO_COLLAPSESEQ logs
    path('filter_representative_2/*') //PRESTO_SPLITSEQ logs
    path('igblast/*') //CHANGEO_MAKEDB logs, empty when V(D)J annotation was skipped
    path('metadata.tsv') //METADATA
    val umi_length
    val cluster_sets
    val skip_vdj_annotation

    output:
    path "Table_sequences_process.tsv", emit: logs
    path "Table*.tsv", emit:tables
    tuple val("${task.process}"), val('python'), eval('python --version 2>&1 | grep -o "[0-9\\. ]\\+"'), emit: versions_python, topic: versions
    tuple val("${task.process}"), val('pandas'), eval('python -c "import pkg_resources; print(pkg_resources.get_distribution(\'pandas\').version)"'), emit: versions_pandas, topic: versions

    script:
    def skip_annotation = skip_vdj_annotation? "--skip_vdj_annotation":""
    // An unset '--umi_length' (-1) is only allowed for the library generation methods that run
    // FilterSeq after assembly on a single read, which is the log shape the no-umi parser reads.
    if (umi_length <= 0) {
        """
        log_parsing_no-umi.py $skip_annotation

        """
    } else {
        def clustersets = cluster_sets? "--cluster_sets":""
        """
        log_parsing.py $clustersets $skip_annotation

        """
    }
}
