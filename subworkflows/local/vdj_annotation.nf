include { CHANGEO_ASSIGNGENES } from '../../modules/local/changeo/changeo_assigngenes'
include { CHANGEO_MAKEDB } from '../../modules/local/changeo/changeo_makedb'
include { CHANGEO_PARSEDB_SPLIT } from '../../modules/local/changeo/changeo_parsedb_split'
// reveal
include { FILTER_QUALITY  } from '../../modules/local/reveal/filter_quality'
include { FILTER_JUNCTION_MOD3  } from '../../modules/local/reveal/filter_junction_mod3'
include { ADD_META_TO_TAB  } from '../../modules/local/reveal/add_meta_to_tab'
include { germlineKey } from './databases'


workflow VDJ_ANNOTATION {

    take:
    ch_fasta // [meta, fasta]
    ch_tsv // [meta, tsv]
    ch_validated_samplesheet
    ch_reference_by_key // channel: [ val(key), path(igblast_base), path(reference_base) ]
    skip_alignment_filter
    productive_only

    main:
    ch_logs = channel.empty()

    // combine(by: 0), not join: many samples share one reference key.
    CHANGEO_ASSIGNGENES (
        ch_fasta
            .map { meta, fasta -> [ germlineKey(meta), meta, fasta ] }
            .combine( ch_reference_by_key, by: 0 )
            .map { _key, meta, fasta, igblast, _reference -> [ meta, fasta, igblast ] }
    )

    ch_logs = ch_logs.mix(CHANGEO_ASSIGNGENES.out.logs)

    // join, not positional pairing: the reference is attached with combine(by: 0),
    // which preserves order within a key but not across keys, so a bare .fmt7
    // channel could otherwise be matched to another sample's reads.
    CHANGEO_MAKEDB (
        CHANGEO_ASSIGNGENES.out.fasta
            .map { meta, fasta -> [ germlineKey(meta), meta, fasta ] }
            .combine( ch_reference_by_key, by: 0 )
            .map { _key, meta, fasta, igblast, reference -> [ meta, fasta, igblast, reference ] }
            .join( CHANGEO_ASSIGNGENES.out.blast )
    )
    ch_logs = ch_logs.mix(CHANGEO_MAKEDB.out.logs)

    ch_assigned_tab = ch_tsv.mix(CHANGEO_MAKEDB.out.tab)

    ch_assignment_logs = CHANGEO_MAKEDB.out.logs

    if (!skip_alignment_filter){
        // Apply quality filters:
        // - locus should match v_call chain
        // - seq alignment min length informative positions 200
        // - max 10% N nucleotides
        FILTER_QUALITY(
            ch_assigned_tab
        )
        ch_for_parsedb_split = FILTER_QUALITY.out.tab
        ch_logs = ch_logs.mix(FILTER_QUALITY.out.logs)
    } else {
        ch_for_parsedb_split = ch_assigned_tab
    }

    if (productive_only) {
        CHANGEO_PARSEDB_SPLIT (
            ch_for_parsedb_split
        )
        ch_logs = ch_logs.mix(CHANGEO_PARSEDB_SPLIT.out.logs)

        // Apply filter: junction length multiple of 3
        FILTER_JUNCTION_MOD3(
            CHANGEO_PARSEDB_SPLIT.out.tab
        )
        ch_logs = ch_logs.mix(FILTER_JUNCTION_MOD3.out.logs)
        ch_repertoire = FILTER_JUNCTION_MOD3.out.tab

    } else {
        ch_repertoire = FILTER_QUALITY.out.tab
    }

    ADD_META_TO_TAB(
        ch_repertoire,
        ch_validated_samplesheet
    )
    ch_logs = ch_logs.mix(ADD_META_TO_TAB.out.logs)


    emit:
    repertoire = ADD_META_TO_TAB.out.tab
    changeo_makedb_logs = ch_assignment_logs
    logs = ch_logs

}
